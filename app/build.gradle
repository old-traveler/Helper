apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.greenrobot.greendao'
apply from: "augment.gradle"
apply from: "../config.gradle"

apply plugin: 'com.sloth.click'
apply plugin: 'com.cuckoo.plugin'

greendao {
  schemaVersion 7
  daoPackage 'com.hyc.helper.gen'
  targetGenDir 'src/main/java'
}

def password = ''

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
password = properties.getProperty("KEY_PASSWORD")

android {
  signingConfigs {
    release {
      storeFile file(RELEASE_STORE_FILE)
      keyAlias = RELEASE_KEY_ALIAS
      keyPassword password
      storePassword password
    }
  }
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.hyc.helper"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 8
    versionName "1.4.2"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      shrinkResources true
      buildConfigField("boolean", "LOG_DEBUG", "false")
      signingConfig signingConfigs.release
      minifyEnabled true
      zipAlignEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      signingConfig signingConfigs.release
      minifyEnabled false
      buildConfigField("boolean", "LOG_DEBUG", "true")
    }
  }
  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }

  flavorDimensions "default"

  productFlavors {
    Helper {
      dimension "default"
    }
  }
}

slothClickConfig {
  enableLife = false
  clickHelperName = "com/hyc/helper/helper/ClickHelper"
}

dependencies {
  configurations {
    all*.exclude group: 'com.google.code.gson'
    all*.exclude group: 'com.squareup.okio'
  }
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  //noinspection GradleCompatible
  implementation depsLibs.support
  testImplementation depsLibs.junit
  androidTestImplementation depsLibs.runner
  androidTestImplementation depsLibs.espresso
  implementation depsLibs.design
  implementation depsLibs.okhttp3
  implementation depsLibs.retrofit2_adapter
  implementation depsLibs.retrofit2
  implementation depsLibs.retrofit2_converter
  implementation depsLibs.rxjava
  implementation depsLibs.rxandroid
  implementation depsLibs.bubbleView
  implementation depsLibs.smartRefresh
  implementation depsLibs.smartRefreshHeader
  implementation depsLibs.glide
  implementation depsLibs.luBan
  implementation depsLibs.greendao
  implementation depsLibs.rxpermissions
  implementation depsLibs.subsampling
  implementation depsLibs.butterknife
  implementation depsLibs.shinebutton
  implementation depsLibs.matisse
  implementation depsLibs.ucrop
  implementation depsLibs.httpLegacy
  implementation depsLibs.bmobIm
  implementation depsLibs.bmobSdk
  implementation depsLibs.HeadZoomLayout
  implementation depsLibs.kotlin
  annotationProcessor depsLibs.butterknife_compiler
  implementation project(path: ':cuckoo-lib')
}
repositories {
  mavenCentral()
}
